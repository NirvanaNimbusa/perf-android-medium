buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.uber:okbuck:0.8.1'
    }
}

apply plugin: 'com.uber.okbuck'

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)
            || "com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
            tasks.matching { task ->
                task.getClass().isAnnotationPresent(ParallelizableTask) || task.getClass().name == "com.android.build.gradle.tasks.factory.AndroidJavaCompile"
            }.all { task ->
                // println "Parallelizable task: ${task.path}"
                task.outputs.cacheIf { true }
            }
        }
    }
}

gradle.addListener(new TaskExecutionAdapter() {
    @Override
    void beforeExecute(Task task) {
        task.outputs.fileProperties.each { property ->
            if (property instanceof org.gradle.api.internal.tasks.CacheableTaskOutputFilePropertySpec) {
                println "  >> OUT: $property.propertyName - $property.outputFile"
            } else {
                println "  >> OUT(non-cacheable): $property.propertyName - $property.propertyFiles.files"
            }
        }
    }
})
